# Document model, layout engine, and formatter.

import("../../gn/sqlite_gen.gni")

# Generated formatter dispatch.
sqlite_gen("formatter_gen") {
  sources = [ "fmt_gen.c" ]
}

source_set("fmt") {
  sources = [
    "doc.c",
    "doc.h",
    "fmt_helpers.c",
    "fmt_helpers.h",
    "fmt_interpret.c",
    "fmt_ops.h",
  ]
  deps = [
    ":formatter_gen",
    "../base:arena",
    "../base:vec",
    "../common",
    "../parser:ast",
    "../parser:token_list",
    "../tokenizer:tokenize",
  ]
}

# Public formatter API.
source_set("formatter") {
  sources = [ "formatter.c" ]
  deps = [
    "../../include/syntaqlite:headers",
    "../parser",
    ":fmt",
  ]
}

# Test binary for format interpreter
executable("fmt_interpret_test") {
  testonly = true
  sources = [
    "fmt_interpret_test.cc",
  ]
  deps = [
    "../../include/syntaqlite:headers",
    "../../third_party:googletest",
    "../../third_party:googletest_main",
    "../parser:ast",
    ":fmt",
  ]
}
