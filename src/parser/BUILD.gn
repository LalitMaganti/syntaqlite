# SQLite parser and AST.
# Regenerate with: python3 python/tools/extract_sqlite.py

import("../../gn/sqlite_gen.gni")

# Generated AST files (builder, nodes header, printer).
sqlite_gen("ast_gen") {
  sources = [
    "ast_builder_gen.c",
    "ast_builder_gen.h",
    "ast_nodes_gen.h",
    "ast_print_gen.c",
  ]
}

# Generated Lemon parser.
sqlite_gen("parser_gen") {
  sources = [ "sqlite_parse_gen.c" ]
}

# AST node types, builder functions, and printer.
source_set("ast") {
  sources = [
    "ast_base.c",
    "ast_base.h",
    "ast_print.h",
  ]
  deps = [
    ":ast_gen",
    "../base:arena",
    "../base:vec",
    "../common",
  ]
}

# Raw token list (populated by parser, consumed by formatter).
source_set("token_list") {
  sources = [
    "token_list.c",
    "token_list.h",
  ]
  deps = [
    "../base:vec",
    "../../include/syntaqlite:headers",
  ]
}

# Lemon-generated parser.
source_set("lemon") {
  sources = [
    "sqlite_parse_gen.h",
  ]
  deps = [
    ":parser_gen",
    "../common",
    "../tokenizer:tokenize",
  ]
  configs += [ "..:lemon_generated" ]
}

# Public parser API.
source_set("parser") {
  sources = [
    "parser.c",
    "parser.h",
  ]
  deps = [
    "../common",
    "../../include/syntaqlite:headers",
    ":ast",
    ":token_list",
    ":lemon",
    "../tokenizer",
  ]
  configs += [ "..:lemon_generated" ]
}
