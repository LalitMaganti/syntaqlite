# SQLite tokenizer and parser for syntaqlite.
# Regenerate with: python3 python/tools/extract_sqlite.py

# Lemon-generated code has unused variables when semantic actions are empty
config("lemon_generated") {
  cflags = [
    "-Wno-unused-parameter",
    "-Wno-unused-variable",
  ]
}

# Header-only generic dynamic array.
source_set("vec") {
  sources = [
    "vec.h",
  ]
}

# Shared arena allocator with offset table.
source_set("arena") {
  sources = [
    "arena.c",
    "arena.h",
  ]
  deps = [
    ":vec",
  ]
}

# SQLite tokenizer (extracted so :fmt can use it for comment injection).
source_set("tokenizer") {
  sources = [
    "sqlite_charmap.h",
    "sqlite_tokenize.c",
    "sqlite_tokens.h",
    "syntaqlite_sqlite_defs.h",
  ]
  configs += [ ":lemon_generated" ]
}

source_set("sqlite") {
  sources = [
    # Generated files
    "sqlite_parse.c",
  ]
  deps = [
    ":tokenizer",
  ]
  configs += [ ":lemon_generated" ]
}

# Token list for collecting raw tokens during parsing.
source_set("token_list") {
  sources = [
    "token_list.c",
    "token_list.h",
  ]
  deps = [
    ":vec",
  ]
}

# AST node types, builder functions, and printer.
# Regenerate with: python3 python/tools/extract_sqlite.py
source_set("ast") {
  sources = [
    # Base types (manually maintained)
    "ast/ast_base.c",
    "ast/ast_base.h",

    # Generated files
    "ast/ast_builder.c",
    "ast/ast_builder.h",
    "ast/ast_nodes.h",
    "ast/ast_print.c",
    "ast/ast_print.h",
  ]
  deps = [
    ":arena",
    ":vec",
  ]
}

# Document model, layout engine, and formatter.
source_set("fmt") {
  sources = [
    "fmt/comment_attach.c",
    "fmt/comment_attach.h",
    "fmt/comment_map.c",
    "fmt/comment_map.h",
    "fmt/doc.c",
    "fmt/doc.h",
    "fmt/doc_layout.c",
    "fmt/doc_layout.h",
    "fmt/fmt.c",
    "fmt/fmt.h",
    "fmt/fmt_options.h",
  ]
  deps = [
    ":arena",
    ":ast",
    ":token_list",
    ":tokenizer",
    ":vec",
  ]
}

# Test binary for AST diff tests
executable("ast_test") {
  testonly = true
  sources = [
    "ast/ast_test_main.c",
  ]
  deps = [
    ":ast",
    ":sqlite",
  ]
  if (is_asan) {
    deps += [ "//gn/standalone:asan_defaults" ]
  }
  configs += [ ":lemon_generated" ]
}

# Test binary for formatter diff tests
executable("fmt_test") {
  testonly = true
  sources = [
    "fmt/fmt_test_main.c",
  ]
  deps = [
    ":ast",
    ":fmt",
    ":sqlite",
    ":token_list",
  ]
  if (is_asan) {
    deps += [ "//gn/standalone:asan_defaults" ]
  }
  configs += [ ":lemon_generated" ]
}
