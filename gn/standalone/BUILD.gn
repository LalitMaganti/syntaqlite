# Default build config for syntaqlite

config("default") {
  cflags = [
    "-Wall",
    "-Wextra",
    "-Werror",
  ]

  cflags_c = [ "-std=c11" ]
  cflags_cc = [ "-std=c++17" ]

  if (is_debug) {
    cflags += [
      "-g",
      "-O0",
    ]
    defines = [ "DEBUG" ]
  } else {
    cflags += [ "-O3" ]
    defines = [ "NDEBUG" ]
  }

  if (is_asan) {
    cflags += [
      "-fsanitize=address",
      "-fno-omit-frame-pointer",
    ]
    ldflags = [ "-fsanitize=address" ]
  }

  if (is_tsan) {
    cflags += [ "-fsanitize=thread" ]
    ldflags = [ "-fsanitize=thread" ]
  }

  if (is_msan) {
    cflags += [
      "-fsanitize=memory",
      "-fno-omit-frame-pointer",
    ]
    ldflags = [ "-fsanitize=memory" ]
  }

  if (extra_cflags != "") {
    cflags += [ extra_cflags ]
  }
  if (extra_cxxflags != "") {
    cflags_cc += [ extra_cxxflags ]
  }

  include_dirs = [
    "../../include",
    "../../",
    "$root_gen_dir/include",
    "$root_gen_dir",
  ]
}

config("no_exceptions") {
  # Exceptions are disabled by default on Windows (Use /EHsc to enable them).
  if (!is_win) {
    cflags_cc = [ "-fno-exceptions" ]
  }
}

config("rtti_cflags") {
  # Generally, we want -fno-rtti. However, this is compatible with some ubsan
  # configurations (specifically, those that use -fsanitize=vptr). Since we
  # don't know if anyone linking in perfetto will use -fsanitize=vptr, don't
  # pass -fno-rtti when is_ubsan is true.
  if (!is_ubsan) {
    if (is_win) {
      cflags_cc = [ "/GR-" ]
    } else {
      cflags_cc = [ "-fno-rtti" ]
    }
  }
}

config("visibility_hidden") {
  if (!is_win) {
    cflags = [ "-fvisibility=hidden" ]
  }
}

source_set("asan_defaults") {
  sources = [ "asan_defaults.c" ]
  configs -= [
    "//gn/standalone:default",
    "//gn/standalone:visibility_hidden",
  ]
  cflags = [ "-fsanitize=address" ]
}

toolchain("clang") {
  tool("cc") {
    depfile = "{{output}}.d"
    command = "$cc_wrapper $cc -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
    depsformat = "gcc"
    description = "CC {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }

  tool("cxx") {
    depfile = "{{output}}.d"
    command = "$cc_wrapper $cxx -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}"
    depsformat = "gcc"
    description = "CXX {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }

  tool("alink") {
    command = "rm -f {{output}} && ar rcs {{output}} {{inputs}}"
    description = "AR {{target_output_name}}{{output_extension}}"
    outputs = [ "{{root_out_dir}}/{{target_output_name}}{{output_extension}}" ]
    default_output_extension = ".a"
    output_prefix = "lib"
  }

  tool("solink") {
    outfile = "{{root_out_dir}}/{{target_output_name}}{{output_extension}}"
    command = "$cxx -shared {{ldflags}} -o $outfile {{inputs}} {{libs}}"
    description = "SOLINK {{output}}"
    outputs = [ outfile ]
    default_output_extension = ".so"
    output_prefix = "lib"
  }

  tool("link") {
    outfile = "{{root_out_dir}}/{{target_output_name}}{{output_extension}}"
    command = "$cxx {{ldflags}} -o $outfile {{inputs}} {{libs}}"
    description = "LINK {{output}}"
    outputs = [ outfile ]
  }

  tool("stamp") {
    command = "touch {{output}}"
    description = "STAMP {{output}}"
  }

  tool("copy") {
    command = "cp -af {{source}} {{output}}"
    description = "COPY {{source}} {{output}}"
  }
}
